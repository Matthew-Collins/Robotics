' Gambas class file

Public fLSpeed As Float                                             'Left speed variable
Public fRSpeed As Float                                             'Right speed variable
Public fSpeed As Float                                              'General speed
Public sDirection As String = "f"                                   'Direction variable
Public sPath As String = "robot"                                    'Location of the python file
Public siPositionU As Short                                         'Up and down position
Public siPositionL As Short                                         'Left and right position
Public fDelay As Float = 1                                          'Delay valu
Public siDummy As Short
Public pProcess As Process

Public Sub Form_Open()                                              'Got to start somewhere!
Dim sShellString As String

Application.MainWindow = Me                                         'This form is the MainWindow so if another widow from this program is open when you close this form both forms will close
Me.Center                                                           'Centre the form on the screen (Not reqiuired from Gambas 3.8.4)
Timer1.delay = 250                                                  'Set the timer delay (1000 = 1 second)
Timer1.Start                                                        'Start the timer
ButtonCentre_Click                                                  'Centre the camera
ButtonCentre_Click                                                  'Centre the camera (seems to need twice sometimes to wake it up!)
sShellString = "cd " & User.Home &/ sPath &/ " && " & " sudo python Sonar.py"
'Print sShellString
pProcess = Shell sShellString


End

Public Sub SliderSpeed_Change()                                     'What to do if the slider changes

fSpeed = SliderSpeed.Value                                          'Change the value of the fSpeed variable to match the Slider value
If Timer1.Enabled = False Then Timer1.Start                         'If the Timer is not running then start it

End

Public Sub DialSteer_Change()                                       'What to do if the Dial is changed

If Timer1.Enabled = False Then Timer1.Start                         'If the Timer is not running then start it

If DialSteer.value = 0 Then                                         'If steering straight ahead
  fRSpeed = fSpeed                                                  'Right motor speed
  fLSpeed = fSpeed                                                  'Left motor speed
Endif

If DialSteer.value < 0 Then                                         'If steering left
  fRSpeed = fSpeed                                                  'Right motor stays at the speed set
  fLSpeed = fSpeed - (fSpeed / 100) * Abs(DialSteer.value)          'Left motor is slowed depending on how far the control is rotated
Endif

If DialSteer.value > 50 Then                                         'If steering right
  fRSpeed = fSpeed - (fSpeed / 100) * DialSteer.Value                'Right motor is slowed depending on how far the control is rotated
  fLSpeed = fSpeed                                                   'Left motor stays at the speed set
Endif

End

Public Sub DriveRobot()                                             'Drive the motors

If fSpeed = 0 Then                                                  'If the speed is zero then..
  ButtonSTOP_Click                                                  'STOP EVERTHING!
  Return                                                            'Get out of here
End If

Shell "cd ~/robot && sudo python motor.py -d " & sDirection & " -ls " & fLSpeed & " -rs " & fRSpeed
                                                                    'Send all the necessary to the Python script
End

Public Sub ButtonSTOP_Click()                                       'What to do if the STOP! button is clicked

Timer1.Stop                                                         'Stop the timer
Shell "cd ~/robot && sudo python stop.py"                           'Send the robot the stop EVERYTHING message!
SliderSpeed.Value = 0                                               'Reset the Speed slider
DialSteer.Value = 0                                                 'Reset the steering to centre

End

Public Sub form_Close()                                             'What to do if the program is closed

ButtonSTOP_Click                                                    'STOP EVERTHING!
pProcess.Kill

End

Public Sub Timer1_Timer()                                           'The timer
Dim sTempDirection As String

DialSteer_Change                                                    'Get the steering settings
DriveRobot                                                          'Keep sending those commands to the robot........

If sDirection = "f" Then                                            'Variable display for development only
  sTempDirection = "Forward"
Else
  sTempDirection = "Reverse"
End If

LabelVar.text = "Direction= " & sTempDirection
LabelVar.text &= "\nSpeed = " & fSpeed
LabelVar.text &= "\nLeft  " & fLSpeed
LabelVar.text &= "\nRight  " & fRSpeed


End

Public Sub RButtons_Click()                                         'What to do if the Radio Buttons are clicked

If RadioForward.Value = True Then                                   'If the 'Forward' button is clicked then..
  sDirection = "f"                                                  'Set the Direction to 'f' (Forward)
Else                                                                'Else..
  sDirection = "b"                                                  'Set the Direction to 'b' (Reverse - backwards)
Endif

End

Public Sub Form_KeyPress()                                          'Keyboard contol
Dim siValue As Short

If Key.code = key.Left Then                                         'If the Left Arrow is pressed
  siValue = DialSteer.value - 5                                     'Decrease value
  If siValue < -100 Then siValue = -100                             'Check new value is valid
  DialSteer.value = siValue                                         'Apply new value to Steering Dial
Endif

If Key.code = key.Right Then                                        'If the Right Arrow is pressed
  siValue = DialSteer.value + 5                                     'Increase value
  If siValue > 100 Then siValue = 100                               'Check value is valid
  DialSteer.value = siValue                                         'Apply new value to Steering Dial
Endif

If Key.code = key.Up Then                                           'If Up Arrow is pressed
  siValue = SliderSpeed.value + 5                                   'Increase value
  If siValue > 100 Then siValue = 100                               'Check new value is valid
  SliderSpeed.value = siValue                                       'Apply new value to Speed Slider
Endif
' 
If Key.code = key.Down Then                                         'If Down Arrow is pressed
  siValue = SliderSpeed.value - 5                                   'Decrease value
  If siValue < 0 Then siValue = 0                                   'Check new value is valid
  SliderSpeed.value = siValue                                       'Apply new value to Speed Slider
Endif

If Key.code = key.Space Then ButtonSTOP_Click                       'If the Space bar is pressed SOTP everything!

If Key.code = key.Shift Then DialSteer.value = 50                   'If the Shift key is pressed centre the steering

If Key.Code = Key["F"] Then RadioForward.Value = True               'If the 'F' key is pressed set drive forward
If Key.Code = Key["R"] Then RadioReverse.Value = True               'If the 'F' key is pressed set drive reverse

End

Public Sub ButtonHelp_Click()

Help.show                                                           'Open the Help form

End

Public Sub ButtonCentre_Click()

SliderLeft.Value = -475                                             'Set the left/right slider to middle position
SliderUP.Value = -400                                               'Set the up/down slider to middle position
SliderUP_MouseUp()                                                  'Set the up and down servo
SliderLeft_MouseUp()                                                'Set the left and right servo

End

Public Sub SliderUP_MouseUp()                                       'Controls up and down movement
Dim sShellString As String                                          'To store the string to shell

siPositionU = Abs(SliderUP.Value)                                   'Set the variable dependent on the slide position

sShellString = "cd " & User.Home &/ sPath &/ " && " & " sudo python servo.py" & " -s " & Str(14) & " -p " & Str(siPositionU) & " -d " & Str(fDelay)
Shell sShellString                                                  'Build the string and shell it

End

Public Sub SliderLeft_MouseUp()                                     'Controls the left and right movement
Dim sShellString As String                                          'To store the string to shell

siPositionL = Abs(SliderLeft.Value)                                 'Set the variable dependent on the slide position

sShellString = "cd " & User.Home &/ sPath &/ " && " & " sudo python servo.py" & " -s " & Str(15) & " -p " & Str(siPositionL) & " -d " & Str(fDelay)
Shell sShellString                                                  'Build the string and shell it
  
End

Public Sub ButtonLeft_Click()

SliderLeft.Value = -575                                             'Set the left/right slider to middle position
SliderUP.Value = -400                                               'Set the up/down slider to middle position
SliderUP_MouseUp()                                                  'Set the up and down servo
SliderLeft_MouseUp()                                                'Set the left and right servo

End

Public Sub ButtonRight_Click()

SliderLeft.Value = -375                                             'Set the left/right slider to middle position
SliderUP.Value = -400                                               'Set the up/down slider to middle position
SliderUP_MouseUp()                                                  'Set the up and down servo
SliderLeft_MouseUp()                                                'Set the left and right servo

End

' Public Sub Timer2_Timer()
'  Exec ["raspistill", "-n", "-t", "1", "-o", "/home/pi/Pictures/Latest.jpg"] 'Wait
'  PictureBox1.Picture = Picture.Load("/home/pi/Pictures/Latest.jpg")
'  'TextLabel1.Caption = Now
' End

Public Sub MenuNo_Click()
Dim fDelay1 As Float = 0.75
Dim siCount As Short

ButtonCentre_Click 
Wait fDelay1

For siCount = 1 To 2
  ButtonRight_Click
  Wait fDelay1
  ButtonLeft_Click
  Wait fDelay1
Next

ButtonCentre_Click

End

Public Sub MenuYes_Click()
Dim fDelay1 As Float = 0.75
Dim sShellString As String
Dim siCount As Short

ButtonCentre_Click
Wait fDelay1

For siCount = 1 To 2
  sShellString = "cd " & User.Home &/ sPath &/ " && " & " sudo python servo.py" & " -s " & Str(14) & " -p " & "266" & " -d " & Str(fDelay)
  Shell sShellString
  Wait fDelay1
  sShellString = "cd " & User.Home &/ sPath &/ " && " & " sudo python servo.py" & " -s " & Str(14) & " -p " & "566" & " -d " & Str(fDelay)
  Shell sShellString
  Wait fDelay1
Next

ButtonCentre_Click

End


Public Sub Timer2_Timer()
Dim sDistance As String
Dim siDistance As Short

sDistance = File.Load(User.Home &/ sPath &/ "Sonar.txt")

Try siDistance = Val(sDistance)

LabelSonar.Text = "Object distance= " & Str(siDistance) & "cms"
  

End
